plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'kotlin-parcelize'
}

apply from: 'libSettings.gradle'

android {
    compileSdk 35
    namespace 'org.libreoffice.androidlib'
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 1
        versionName "1.0"

        resValue "string", "app_name", "${liboAppName}"
        buildConfigField "String", "GIT_COMMIT", "\"${liboOVersionHash}\""
        buildConfigField "boolean", "GOOGLE_PLAY_ENABLED", "${liboGooglePlay}"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            zipAlignEnabled true
            debuggable false
            renderscriptOptimLevel 3

            ndk {
                debugSymbolLevel 'NONE'
            }
        }
        debug {
            resValue "string", "app_name", "${liboAppName} Debug"
            debuggable true
        }
    }

    packagingOptions {
        pickFirst '**/libc++_shared.so'
        pickFirst '**/libjsc.so'
        exclude 'META-INF/**'
        exclude 'kotlin/**'
        exclude '**/*.kotlin_metadata'
        exclude '**/*.kotlin_module'
        exclude '**/*.kotlin_builtins'
        exclude 'DebugProbesKt.bin'
        exclude 'kotlin-tooling-metadata.json'
        exclude 'lib/*/libc++_shared.so'
        exclude 'lib/*/libjsc.so'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
        exclude 'assets/webkit/**'
        exclude 'assets/unpack/**'
        merge '**/LICENSE'
        merge '**/LICENSE.txt'
        merge '**/NOTICE'
        merge '**/NOTICE.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        buildConfig = true
        viewBinding = true
    }

    splits {
        abi {
            enable true
            reset()
            include 'arm64-v8a'
        }
    }

    lint {
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.2.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0"
    implementation 'com.google.android.gms:play-services-ads-api:24.7.0'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "ezt.documents"
                artifactId = "libreoffice"
                version = "1.0.0-beta9"

                artifact(tasks.getByName("bundleReleaseAar"))

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each { dependency ->
                        if (dependency.group != null && dependency.version != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', dependency.group)
                            dependencyNode.appendNode('artifactId', dependency.name)
                            dependencyNode.appendNode('version', dependency.version)
                            dependencyNode.appendNode('scope', 'compile')
                        }
                    }

                    // Add Facebook Shimmer dependency explicitly
                    def shimmerDep = dependenciesNode.appendNode('dependency')
                    shimmerDep.appendNode('groupId', 'com.facebook.shimmer')
                    shimmerDep.appendNode('artifactId', 'shimmer')
                    shimmerDep.appendNode('version', '0.5.0')
                    shimmerDep.appendNode('scope', 'compile')
                }
            }
        }
        repositories {
            maven {
                name = "TanhB02"
                url = uri("https://maven.pkg.github.com/TanhB02/EZT_Office")
                credentials {
                    username = "TanhB02"
                    password = "***REMOVED***"
                }
            }
        }
    }
}


tasks.withType(JavaExec).configureEach {
    jvmArgs += ['--add-opens=java.base/java.util.concurrent=ALL-UNNAMED']
}

tasks.named('publish') {
    dependsOn 'assembleRelease'
}
